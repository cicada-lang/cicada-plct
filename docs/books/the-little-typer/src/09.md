---
title: 9. Double Your Money, Get Twice as Much
date: 2021-05-26
---

# Exercises

```cicada
import { Nat, nat_rec, nat_ind, zero, one, two, three } from "./02.md"
import { cong } from "./08.md"
import { Vector, vector_head, vector_tail } from "./06.md"

function add(x: Nat, y: Nat): Nat {
  return nat_rec(x, y, (_prev, almost) => Nat.add1(almost))
}

// NOTE increase
function incr(x: Nat): Nat {
  return nat_rec(x, one, (_prev, almost) => add(one, almost))
}

compute the(Equal(Nat, incr(three), Nat.add1(three)), refl)

function incr_add1_equal_motive(n: Nat): Type {
  return Equal(Nat, incr(n), Nat.add1(n))
}

let incr_add1_equal_base: Equal(Nat, incr(zero), Nat.add1(zero)) = refl

let incr_add1_equal_step_t: Type = (
  prev: Nat,
  almost: Equal(Nat, incr(prev), Nat.add1(prev))
) -> Equal(Nat, Nat.add1(incr(prev)), Nat.add1(Nat.add1(prev)))

// NOTE Let's prove `incr_add1_equal_step` use `replace` directly.
function incr_add1_equal_step(
  prev: Nat,
  almost: Equal(Nat, incr(prev), Nat.add1(prev))
): Equal(Nat, Nat.add1(incr(prev)), Nat.add1(Nat.add1(prev))) {
  function motive(n: Nat): Type {
    return Equal(Nat, Nat.add1(incr(prev)), Nat.add1(n))
  }

  let base: Equal(Nat, Nat.add1(incr(prev)), Nat.add1(incr(prev))) = refl

  return replace(almost, motive, base)
}

function incr_add1_equal(n: Nat): Equal(Nat, incr(n), Nat.add1(n)) {
  return nat_ind(
    n,
    incr_add1_equal_motive,
    incr_add1_equal_base,
    incr_add1_equal_step)
}

compute incr_add1_equal(zero)
compute incr_add1_equal(one)
compute incr_add1_equal(two)
compute incr_add1_equal(three)

function double(n: Nat): Nat {
  return nat_rec(n, zero, (_prev, almost) => Nat.add1(Nat.add1(almost)))
}

compute double(one)
compute double(two)
compute double(three)

function twice(n: Nat): Nat {
  return add(n, n)
}

compute twice(one)
compute twice(two)
compute twice(three)

function add1_add_commute_on_2nd_arg(n: Nat, j: Nat): Equal(Nat, Nat.add1(add(n, j)), add(n, Nat.add1(j))) {
  function motive(j: Nat, target: Nat): Type {
    return Equal(Nat, Nat.add1(add(target, j)), add(target, Nat.add1(j)))
  }

  let base: Equal(Nat, Nat.add1(add(zero, j)), add(zero, Nat.add1(j))) = refl

  function step(
    prev: Nat,
    almost: Equal(Nat, Nat.add1(add(prev, j)), add(prev, Nat.add1(j)))
  ): Equal(Nat, Nat.add1(Nat.add1(add(prev, j))), Nat.add1(add(prev, Nat.add1(j)))) {
    return cong(almost, add(one))
  }

  return nat_ind(n, motive(j), base, step)
}

function twice_double_equal_step(
  prev: Nat,
  almost: Equal(Nat, add(prev, prev), double(prev))
): Equal(Nat, Nat.add1(add(prev, Nat.add1(prev))), Nat.add1(Nat.add1(double(prev)))) {
  let target: Equal(Nat, Nat.add1(add(prev, prev)), add(prev, Nat.add1(prev))) =
    add1_add_commute_on_2nd_arg(prev, prev)

  function motive(target: Nat): Type {
    return Equal(Nat, Nat.add1(target), Nat.add1(Nat.add1(double(prev))))
  }

  let base: Equal(Nat, Nat.add1(Nat.add1(add(prev, prev))), Nat.add1(Nat.add1(double(prev)))) =
    cong(almost, add(two))

  return replace(target, motive, base)
}

function twice_double_equal(n: Nat): Equal(Nat, twice(n), double(n)) {
  function motive(target: Nat): Type {
    return Equal(Nat, twice(target), double(target))
  }

  let base: Equal(Nat, zero, zero) = refl

  return nat_ind(n, motive, base, twice_double_equal_step)
}

compute twice_double_equal(three)

let twice_double_equal_on_three: Equal(Nat, twice(three), double(three)) = twice_double_equal(three)
let twice_double_equal_on_three_again: Equal(Nat, twice(three), double(three)) = refl

function vector_double(
  E: Type,
  length: Nat,
): (Vector(E, length)) -> Vector(E, double(length)) {
  function motive(target: Nat): Type {
     return (Vector(E, target)) -> Vector(E, double(target))
  }

  let base: (Vector(E, zero)) -> Vector(E, double(zero)) = (vector) => Vector.null

  function step(prev: Nat, doubler: motive(prev)): motive(Nat.add1(prev)) {
    return (vector) => {
      let head = vector_head(vector)
      return Vector.cons(head, Vector.cons(head, doubler(vector_tail(vector))))
    }
  }

  return nat_ind(length, motive, base, step)
}

compute vector_double(String, three, Vector.cons("a", Vector.cons("b", Vector.cons("c", Vector.null))))

function symm(T: Type, from:T, to: T, target: Equal(T, from, to)): Equal(T, to, from) {
  return replace(target, (x) => Equal(T, x, from) , refl)
}

function vector_twice(
  E: Type,
  length: Nat,
  vector: Vector(E, length)
): Vector(E, twice(length)) {
  let target: Equal(Nat, double(length), twice(length)) =
    symm(Nat, twice(length), double(length), twice_double_equal(length))

  function motive(length: Nat): Type {
    return Vector(E, length)
  }

  let base = vector_double(E, length, vector)

  return replace(target, motive, base)
}

compute vector_twice(String, three, Vector.cons("a", Vector.cons("b", Vector.cons("c", Vector.null))))
```

# Note about `replace`

But if `replace` replaces the `from` with
the `to`, why should we abstract over the `to`,
rather than the `from`?

Because first we know `motive(to)`,
thus use abstract `to` to get `motive`,
then we try to find exp of type `motive(from)`,
to apply `replace` to get `motive(to)`.
