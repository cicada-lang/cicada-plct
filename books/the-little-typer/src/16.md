---
title: 16. If It's All the Same to You
---

# dependency

```cicada
import { Nat, nat_rec, nat_ind } from "./02.md"
import { zero, one, two, three, four, five, six, seven, eight, nine, ten } from "./02.md"
import { Either } from "./13.md"
```

# `zero_equal_decide`

```cicada
import { Decide, use_nat_equal } from "./15.md"

function zero_equal_decide(n: Nat): Decide(Equal(Nat, zero, n)) {
  function motive(target: Nat): Type {
    return Decide(Equal(Nat, zero, target))
  }

  let base: Decide(Equal(Nat, zero, zero)) = Either.inl(refl)

  function step(
    prev: Nat,
    _almost: Decide(Equal(Nat, zero, prev)),
  ): Decide(Equal(Nat, zero, Nat.add1(prev))) {
    return Either.inr((equal) => use_nat_equal(zero, Nat.add1(prev), equal))
  }

  return nat_ind(n, motive, base, step)
}

compute zero_equal_decide(zero)
compute zero_equal_decide(one)
compute zero_equal_decide(two)
compute zero_equal_decide(three)
```

# `nat_equal_decide`

```cicada
import { add } from "./03.md"
import { cong, cong_explicit } from "./08.md"
import { sub1_equal } from "./15.md"

function nat_equal_decide_motive(target: Nat): Type {
  return (j: Nat) -> Decide(Equal(Nat, target, j))
}

let nat_equal_decide_base: nat_equal_decide_motive(zero) = (j) => zero_equal_decide(j)


function add1_equal_decide(
  x: Nat, y: Nat,
  eq_or_not: Decide(Equal(Nat, x, y)),
): Decide(Equal(Nat, Nat.add1(x), Nat.add1(y))) {
  return recursion (eq_or_not) {
    case inl(equal) => {
      let h = cong(equal, add(one))
      return Either.inl(h)
    }
    case inr(equal_absurd) => {
      return Either.inr((equal) => equal_absurd(sub1_equal(x, y, equal)) )
    }
  }
}

function nat_equal_decide_step(
  prev: Nat,
  almost: (j: Nat) -> Decide(Equal(Nat, prev, j)),
  j: Nat,
): Decide(Equal(Nat, Nat.add1(prev), j)) {
  function motive(target: Nat): Type {
    return Decide(Equal(Nat, Nat.add1(prev), target))
  }

  let base: Decide(Equal(Nat, Nat.add1(prev), zero)) =
    Either.inr((equal) => use_nat_equal(Nat.add1(prev), zero, equal) )

  function step(
    prev_j: Nat,
    // NOTE This inner inductive hypothesis will not be used, use the outer one.
    _almost: Decide(Equal(Nat, Nat.add1(prev), prev_j)),
  ): Decide(Equal(Nat, Nat.add1(prev), Nat.add1(prev_j))) {
    return add1_equal_decide(prev, prev_j, almost(prev_j))
  }

  return nat_ind(j, motive, base, step)
}

function nat_equal_decide(n: Nat, j: Nat): Decide(Equal(Nat, n, j)) {
  return nat_ind(
    n,
    nat_equal_decide_motive,
    nat_equal_decide_base,
    nat_equal_decide_step
  )(j)
}

compute nat_equal_decide(zero, zero)
compute nat_equal_decide(one, zero)
compute nat_equal_decide(zero, one)
compute nat_equal_decide(six, four)
```
