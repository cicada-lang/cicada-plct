---
title: 8. Pick a Number, Any Number
date: 2021-05-24
---

# Two ways to add1 -- `incr` and `add1`

```cicada
import { Nat, nat_rec, zero, one, two, three } from "./02.md"

function add(x: Nat, y: Nat): Nat {
  return nat_rec(x, y, (_prev, almost) => Nat.add1(almost))
}

// NOTE increase
function incr(x: Nat): Nat {
  return nat_rec(x, one, (_prev, almost) => add(one, almost))
}

compute the(Equal(Nat, incr(three), add(one)(three)), refl)
```

`add(one)` is equal to `add1` by reflection.

```cicada
function add_1_add1_equal(n: Nat): Equal(Nat, add(one, n), Nat.add1(n)) {
  return refl
}
```

But `add(one)` is not equal to `incr` by reflection, we need to prove it.

```cicada counterexample
compute the(Equal((Nat) -> Nat, add(one), incr), refl)
```

# `replace` and `cong`

We will use `replace` to define `cong`

```cicada
let replace_t: Type = (
  implicit X: Type,
  implicit from: X,
  implicit to: X,
  target: Equal(X, from, to),
  motive: (X) -> Type,
  base: motive(from),
) -> motive(to)

function cong(
  implicit X: Type,
  implicit from: X,
  implicit to: X,
  target: Equal(X, from, to),
  implicit Y: Type,
  f: (X) -> Y,
): Equal(Y, f(from), f(to)) {
  return replace(
    target,
    (to) => Equal(Y, f(from), f(to)),
    refl)
}
```

# `incr_add1_equal`

```cicada
function incr_add1_equal_motive(n: Nat): Type {
  return Equal(Nat, incr(n), Nat.add1(n))
}

let incr_add1_equal_base: Equal(Nat, incr(zero), Nat.add1(zero)) = refl

let incr_add1_equal_step_t: Type = (
  prev: Nat,
  almost: Equal(Nat, incr(prev), Nat.add1(prev))
) -> Equal(Nat, Nat.add1(incr(prev)), Nat.add1(Nat.add1(prev)))


function incr_add1_equal_step(
  prev: Nat,
  almost: Equal(Nat, incr(prev), Nat.add1(prev))
): Equal(Nat, Nat.add1(incr(prev)), Nat.add1(Nat.add1(prev))) {
  return cong(almost, add(one))
}

function incr_add1_equal(n: Nat): Equal(Nat, incr(n), Nat.add1(n)) {
  return induction (n) {
    motive incr_add1_equal_motive
    case zero => incr_add1_equal_base
    case add1(prev, almost) => cong(almost.prev, add(one))
  }
}
```

Some example proofs.

```cicada
compute incr_add1_equal(zero)
compute incr_add1_equal(one)
compute incr_add1_equal(two)
compute incr_add1_equal(three)
```

# `cong_opts` -- with option arguments

```cicada
function cong_opts(
  opts: class {
    X: Type,
    Y: Type,
    from: X,
    to: X,
  },
  target: Equal(opts.X, opts.from, opts.to),
  f: (opts.X) -> opts.Y,
): Equal(opts.Y, f(opts.from), f(opts.to)) {
  return replace(
    target,
    (to) => Equal(opts.Y, f(opts.from), f(to)),
    refl)
}

function incr_add1_equal_step_by_cong_opts(
  prev: Nat,
  almost: Equal(Nat, incr(prev), Nat.add1(prev))
): Equal(Nat, Nat.add1(incr(prev)), Nat.add1(Nat.add1(prev))) {
  return cong_opts({
    X: Nat,
    Y: Nat,
    from: incr(prev),
    to: Nat.add1(prev),
  }, almost, add(one))
}

function incr_add1_equal_by_cong_opts(n: Nat): Equal(Nat, incr(n), Nat.add1(n)) {
  return induction (n) {
    motive incr_add1_equal_motive
    case zero => incr_add1_equal_base
    case add1(prev, almost) => incr_add1_equal_step_by_cong_opts(prev, almost.prev)
  }
}
```

# `cong_explicit` -- without implicit arguments

```cicada
function cong_explicit(
  X: Type, from: X, to: X,
  target: Equal(X, from, to),
  Y: Type,
  f: (X) -> Y,
): Equal(Y, f(from), f(to)) {
  return replace(
    target,
    (to) => Equal(Y, f(from), f(to)),
    refl)
}

function incr_add1_equal_step_by_cong_explicit(
  prev: Nat,
  almost: Equal(Nat, incr(prev), Nat.add1(prev))
): Equal(Nat, Nat.add1(incr(prev)), Nat.add1(Nat.add1(prev))) {
  return cong_explicit(Nat, incr(prev), Nat.add1(prev), almost, Nat, add(one))
}

function incr_add1_equal_by_cong_explicit(n: Nat): Equal(Nat, incr(n), Nat.add1(n)) {
  return induction (n) {
    motive incr_add1_equal_motive
    case zero => incr_add1_equal_base
    case add1(prev, almost) => incr_add1_equal_step_by_cong_explicit(prev, almost.prev)
  }
}
```

# Sameness is judgment that can be expressed by a type constructor

Sameness is indeed a judgment. But, Another sandwich?
with a new type constructor, types can
express a new idea called equality.

We can express two functions always find the same result.

This is a new perspective on types.

Types can be read as statements (propositions).

Xie: Recall that,
judgment is attitude person take towards expression,
judgment is attitude person take when thinking about expression,
maybe all form of judgment can be expressed by types (types are again expression),
but we should not mix the two terms "judgment" and "type",
because in some moment, maybe we do not know
how to express a form of judgment as type.

Judgments correspond to the concepts we used in our implementation of a language,
in a implementation these concepts will be implemented as functions like:

- `check`
- `check_type`
- `check_same`

But "forall" and "exists" are special, they do not correspond to functions in implementation.

Creating expressions that capture the ideas behind a form of judgment
is sometimes called internalizing the form of judgment.

# judging a statement to be true

If a type can be read as a statement,
then judging the statement to be true means that
there is an expression with that type.

Thus in this view,
truth means that we have evidenced,
and this evidence is called a proof.

# Definition of Neutral again

Expressions that are not values
and cannot yet be evaluated due to a variable
are called neutral.

A more precise way to define neutral
expressions is to start with the simplest
neutral expressions and build from there.

Variables are neutral, unless they refer to definitions,
because a defined name is the same as its definition.

Also, if the target of an eliminator expression is neutral,
then the entire expression is neutral.

# Type theory encode patterns of reasoning

Judgments often can be mechanically checked using relatively simple rules.
This is why judgments are a suitable basis for knowledge.

Expressions, however, can encode interesting patterns of reasoning,
such as using induction to try each possibility
for the variable in a neutral expression.

# Total function

Why it is important that all functions should be total?
Because only total functions can be viewed as proof.
